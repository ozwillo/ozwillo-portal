buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.moowork.gradle:gradle-node-plugin:1.0.1"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'

jar {
    baseName = 'ozwillo-portal'
    version = '1.50.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

node {
    version = '9.4.0'
    yarnVersion = '1.3.2'
    download = true
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    /* Spring Boot managed dependencies
         do not specify a version to let them live along with Spring Boot without extra hassle */
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('com.fasterxml.jackson.dataformat:jackson-dataformat-xml')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-joda')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    compile('com.hazelcast:hazelcast')
    compile('com.hazelcast:hazelcast-spring')
    compile('joda-time:joda-time')
    compile('org.apache.httpcomponents:httpclient')
    compile('org.apache.httpcomponents:httpcore')
    compile('org.thymeleaf.extras:thymeleaf-extras-conditionalcomments')
    compile('org.apache.commons:commons-csv:1.5')

    compile('com.ozwillo:ozwillo-java-spring-integration:1.25.7')

    /* Our specific dependencies */
    compile('de.javakaffee.msm:memcached-session-manager:2.1.1')
    compile('de.javakaffee.msm:memcached-session-manager-tc8:2.1.1')
    compile('org.apache.tika:tika-core:1.16')

    // java.mail API replacement (Apache 2.0 License)
    compile('org.apache.geronimo.javamail:geronimo-javamail_1.4_mail:1.8.4')
    compile('org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.1')

    testCompile('org.springframework.boot:spring-boot-starter-test')

    runtime('commons-fileupload:commons-fileupload:1.3.3')


    /* Runtime dependencies brought by ozwillo-java-spring-integration */
    runtime('com.google.code.gson:gson:2.8.2')
    runtime('com.ibm.icu:icu4j:60.1')
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

bootRun {
    // needed for hot reload of thymeleaf templates
    addResources = true

    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "dev"
}

springBoot {
    // exclude Spring Boot Devtools from the fat jar
    excludeDevtools = true

    executable = true
    embeddedLaunchScriptProperties =
            [initInfoDescription: 'Ozwillo Portal',
             initInfoShortDescription: 'Ozwillo Portal',
             initInfoProvides: 'ozwillo-portal']
}

configure(tasks.processResources) {
    // we don't want source JSX in the jar, we only need their transpiled, bundled version in build/
    exclude 'public/js/'
    exclude 'public/css/'
}

task frontBundle(type: YarnTask) {
    args = ['run', 'build']
    doLast {
        copy {
            from "${sourceSets.main.resources.srcDirs[0]}/public/build"
            into "${sourceSets.main.output.resourcesDir}/public/build"
        }
    }
}

jar.dependsOn(frontBundle)
frontBundle.dependsOn(yarn_install)
