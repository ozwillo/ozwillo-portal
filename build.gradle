buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.moowork.node" version "0.11"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'ozwillo-portal'
    version = '1.47.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

node {
    version = '5.5.0'
    npmVersion = '3.3.12'
    download = true
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    /* Spring Boot managed dependencies
         do not specify a version to let them live along with Spring Boot without extra hassle */
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('com.fasterxml.jackson.dataformat:jackson-dataformat-xml')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-joda')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    compile('com.hazelcast:hazelcast')
    compile('com.hazelcast:hazelcast-spring')
    compile('joda-time:joda-time')
    compile('org.apache.httpcomponents:httpclient')
    compile('org.apache.httpcomponents:httpcore')
    compile('org.thymeleaf.extras:thymeleaf-extras-conditionalcomments')

    compile project('lib:ozwillo-java-spring-integration')

    /* Our specific dependencies */
    compile('de.javakaffee.msm:memcached-session-manager:1.8.3')
    compile('de.javakaffee.msm:memcached-session-manager-tc8:1.8.3')
    compile('org.apache.tika:tika-core:1.11')
    // java.mail API replacement (Apache 2.0 License)
    compile('org.apache.geronimo.javamail:geronimo-javamail_1.4_mail:1.8.4')
    compile('org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.0')

    testCompile('org.springframework.boot:spring-boot-starter-test')

    runtime('commons-fileupload:commons-fileupload:1.3.1')

    /* Runtime dependencies brought by ozwillo-java-spring-integration */
    runtime('com.google.code.gson:gson:2.5')
    runtime('com.ibm.icu:icu4j:56.1')
    // TODO : upgrade to a recent version
    runtime('com.nimbusds:nimbus-jose-jwt:2.22.1')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

bootRun {
    // needed for hot reload of thymeleaf templates
    addResources = true
}

springBoot {
    // exclude Spring Boot Devtools from the fat jar
    excludeDevtools = true

    executable = true
    embeddedLaunchScriptProperties =
            [initInfoDescription: 'Ozwillo Portal',
             initInfoShortDescription: 'Ozwillo Portal',
             initInfoProvides: 'ozwillo-portal']
}

configure(tasks.processResources) {
    // we don't want source JSX in the jar, we only need their transpiled, bundled version in build/
    exclude 'public/jsx/'
    exclude 'public/css/'
}

task frontBundle(type: NpmTask) {
    args = ['run', 'build']
}

processResources.dependsOn(frontBundle)
frontBundle.dependsOn(npmInstall)
